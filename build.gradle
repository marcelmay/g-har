apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'codenarc'

version = '1.0'
group = 'de.m3y.ghar'

// Artifacts
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.3.6'
  compile 'org.codehaus.groovy:groovy-json:2.3.6'
  testCompile 'org.codehaus.groovy:groovy-test:2.3.6'
}

test {
  testLogging {
    exceptionFormat = 'full'
  }
}

codenarc {
  configFile = rootProject.file('config/codenarc/codenarc.groovy')
  reportFormat = System.properties['codeNarcOutput'] ?: 'html'
}

uploadArchives {
    repositories {
        mavenDeployer {
            name = 'Bintry Repository'
            repository(id: 'bintray-repository', url: 'https://api.bintray.com/maven/marcel-may/maven/g-har') {
                authentication(userName: bintrayUser, password: bintrayKey)
            }
        }
    }
}
